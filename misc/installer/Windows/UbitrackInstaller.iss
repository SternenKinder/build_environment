; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "UbiTrack"
#define MyAppVersion "0.2 beta"
#define MyAppPublisher "Technische Universitat München"
#define MyAppURL "http://campar.in.tum.de/UbiTrack"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppID={{B23274A0-A83D-47AB-BFE8-98B8BC7353C4}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\libraries\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=true
LicenseFile=..\..\lgpl.txt
OutputDir=.\
OutputBaseFilename=UbiTrack_Installer
Compression=lzma/Max
SolidCompression=true
WizardImageBackColor=clAqua
UninstallDisplayName=UbiTrack
ChangesEnvironment=true
RestartIfNeededByRun=False
VersionInfoCompany=TUM FAR
VersionInfoDescription=UbiTrack library
VersionInfoTextVersion={#MyAppVersion}

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
;Name: modifypath; Description: "&Install UbiTrack for the Current User"; Flags: unchecked

Name: "allUsers"; Description: "&All users"; GroupDescription: "Add UbiTrack to Path:"; Flags: exclusive; Components: Ubitrack64
Name: "currentUser"; Description: "Just &me"; GroupDescription: "Add UbiTrack to Path:"; Flags: exclusive unchecked; Components: Ubitrack64

Name: "allUsers32"; Description: "All users 32bit"; GroupDescription: "Add UbiTrack to Path:"; Flags: exclusive unchecked; Components: Ubitrack32
Name: "currentUser32"; Description: "Just me 32bit"; GroupDescription: "Add UbiTrack to Path:"; Flags: exclusive unchecked; Components: Ubitrack32
Name: "noUser"; Description: "&None"; GroupDescription: "Add UbiTrack to Path:"; Flags: exclusive unchecked

[Files]
;UbiTrack files 64 bit
Source: "..\..\..\lib\*"; DestDir: "{app}\UbiTrack\lib"; Flags: ignoreversion 64bit createallsubdirs recursesubdirs; Components: UbiTrack64
Source: "..\..\..\bin\*"; DestDir: "{app}\UbiTrack\bin"; Flags: ignoreversion 64bit createallsubdirs recursesubdirs; Components: UbiTrack64
;UbiTrack files 32 bit
Source: "..\..\..\lib32\*"; DestDir: "{app}\UbiTrack\lib32"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: UbiTrack32
Source: "..\..\..\bin32\*"; DestDir: "{app}\UbiTrack\bin32"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: UbiTrack32
;UbiTrack common files
Source: "..\..\..\include\*"; DestDir: "{app}\UbiTrack\include"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: UbiTrack64 UbiTrack32
Source: "..\..\..\doc\*"; DestDir: "{app}\UbiTrack\doc"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: UbiTrack64 UbiTrack32
;Trackman files
Source: "..\..\..\..\trackman\bin\*"; DestDir: "{app}\Trackman\bin"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: Trackman
Source: "..\..\..\..\trackman\lib\*"; DestDir: "{app}\Trackman\lib"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: Trackman
Source: "..\..\..\..\trackman\doc\*"; DestDir: "{app}\Trackman\doc"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: Trackman
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{group}\Trackman"; Filename: "startTrackman.bat"; WorkingDir: "{app}\trackman\bin\"; Components: Trackman

[Types]
Name: full; Description: Install UbiTrack+TrackMan
Name: UbiTrack; Description: Install only UbiTrack Libraries
Name: TrackMan; Description: Install only Trackman 
Name: custom; Description: Custom installation; Flags: iscustom
[Components]
Name: Ubitrack32; Description: Ubitrack 32 bit; Types: Ubitrack full
Name: Ubitrack64; Description: Ubitrack 64 bit; Types: Ubitrack full
Name: Trackman; Description: Trackman; Types: Trackman full

[Registry]
Root: "HKCU"; Subkey: "Environment"; ValueType: string; ValueName: "UBITRACK_PATH"; ValueData: "{app}\UbiTrack\bin"; Flags: uninsdeletevalue; Components: Ubitrack64; Tasks: currentUser
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "UBITRACK_PATH"; ValueData: "{app}\UbiTrack\bin"; Flags: uninsdeletevalue; Components: Ubitrack64; Tasks: allUsers
Root: "HKCU"; Subkey: "Environment"; ValueType: string; ValueName: "UBITRACK_PATH"; ValueData: "{app}\UbiTrack\bin32"; Flags: uninsdeletevalue; Components: Ubitrack32; Tasks: currentUser32
Root: "HKLM"; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "UBITRACK_PATH"; ValueData: "{app}\UbiTrack\bin32"; Flags: uninsdeletevalue; Components: Ubitrack32; Tasks: allUsers32

[Code]
var 	
  ModPathName : String;
  ModPathType : String;

function ModPathDir(): TArrayOfString;
begin
	setArrayLength(Result, 1);
  Result[0] := '%UBITRACK_PATH%'; //ExpandConstant('{app}\UbiTrack\bin');	
end;

#include "modpath.iss"
#include "trackmanConfig.iss"

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Result := True;
  if CurPageID = wpSelectTasks then begin
     if IsTaskSelected('allUsers') or IsTaskSelected('noUser') then begin
       ModPathName := 'allUsers'; 
       ModPathType := 'system';
     end
     else if IsTaskSelected('currentUser') then begin
       ModPathName := 'currentUser'; 
       ModPathType := 'user';
     end
     else if IsTaskSelected('allUsers32') then begin
       ModPathName := 'allUsers32'; 
       ModPathType := 'system';     
     end
     else if IsTaskSelected('currentUser32') then begin
       ModPathName := 'currentUser32'; 
       ModPathType := 'user';
     end;
      
      
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
	taskname:	String;  
  batFile : String;
begin
	taskname := ModPathName;
	if CurStep = ssPostInstall then
  begin
    if IsTaskSelected(taskname) then
			ModPath();
    
    if IsComponentSelected('Trackman') and IsComponentSelected('UbiTrack32') then
    begin      
      batFile :=  ExpandConstant('{app}\Trackman\bin\startTrackman32.bat');
      CreateTrackingConfigFile(True, batFile);    
    end;
    
    if IsComponentSelected('Trackman') and IsComponentSelected('UbiTrack64') then
    begin      
      batFile :=  ExpandConstant('{app}\Trackman\bin\startTrackman.bat');
      CreateTrackingConfigFile(False, batFile);    
    end;
  end;
		
    
end;
